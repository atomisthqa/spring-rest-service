/*
	Published editor that will be a generator entry point.
	Corresponds to old spring-rest-service template.
	Dependent on known package and class naming of the start point project.
	Parameters from the editors used will propagate to
	entry point, except for old_class and old_package, which are overridden.
*/
@tag "java"
@tag "spring"
@tag "spring-boot"
@tag "spring-rest"
@description "Creates a new Spring Boot Rest Microservice project"
@generator "Spring Boot Rest Microservice"
editor NewSpringBootRestMicroserviceProject

uses atomist.common-editors.UpdateReadme
uses atomist.common-editors.PomParameterizer
uses atomist.common-editors.PackageMove
uses atomist.common-editors.ClassRenamer

@maxLength 21
@description "Name of the new project."
@displayName "project name"
param name: @project_name

@description "Description of the new project"
@displayName "description"
param description: .*

@description "Group id: Used by Maven"
@displayName "Group ID"
param group_id: @group_id

@default "0.0.1-SNAPSHOT"
@displayName "Semantic Version"
@optional
@description "Version of your project. Used in Maven POM"
param version: @semantic_version

@default "com.myorg"
@displayName "Root Package"
@optional
@description "Root package for your generated source"
param new_package: @java_package

@description "New name for the class"
@displayName "Class Name"
@validInput "Java class names may contain alphanumeric characters, $ and _"
param new_class: @java_class

# Pattern to replace in old class name.
let old_class = "SpringRest"

# Root package of the old file
let old_package = "com.atomist.springrest"

# Now we invoke generic editors that do the actual work

SwitchReadmes
PomParameterizer artifact_id = name
PackageMove
ClassRenamer
AddCreationDateToReadme
AddLogback


editor SwitchReadmes

param name: @project_name

@description "Description of the new project"
@displayName "Description"
param description: .*

let template = "readme.vm"

with project p
	do merge template to "README.md"


editor AddCreationDateToReadme

with file f when path = "README.md"
  do replace "{{creation_date}}" { new Date().toISOString().split('T')[0] }


editor AddLogback

@description "Root Package of the Project"
@displayName "Project Root Package"
param new_package: @java_package

let template = "logback.vm"

with project p
	do merge template to "src/main/resources/logback.xml"
